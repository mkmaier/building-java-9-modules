subprojects {
    repositories {
        jcenter()
    }

    afterEvaluate {
        compileJava {
            inputs.property("moduleName", moduleName)
            options.compilerArgs = [
                '--module-path', classpath.asPath,
            ]
        }

        compileTestJava {
            inputs.property("moduleName", moduleName)
            options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'junit',
                '--add-reads', "$moduleName=junit",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
            ]
        }

        test {
            inputs.property("moduleName", moduleName)
            jvmArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'ALL-MODULE-PATH',
                '--add-reads', "$moduleName=junit",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
            ]
        }
    }
}
